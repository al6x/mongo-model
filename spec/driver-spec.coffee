require './helper'
_ = require('underscore')._
mongo = require '../lib/driver'

describe "Driver Db", ->
  withMongo()

  it.sync "should provide handy shortcuts to collections", ->
    $db.collection('test').name.should be: 'test'

  it.sync "should list collection names", ->
    $db.collection('alpha').create a: 'b'
    $db.collectionNames().should include: 'alpha'

  it.sync "should clear database", ->
    $db.collection('alpha').insert a: 'b'
    $db.collectionNames().should include: 'alpha'
    $db.clear()
    $db.collectionNames().shouldNot include: 'alpha'

describe "Driver Collection", ->
  withMongo()

  describe "CRUD", ->
    it.sync "should create", ->
      units = $db.collection 'units'
      unit = name: 'Probe',  status: 'alive'
      units.create(unit).shouldNot be: null
      unit._id.shouldNot be: null
      units.first(name: 'Probe').status.should be: 'alive'

    it.sync "should update", ->
      units = $db.collection 'units'
      unit = name: 'Probe',  status: 'alive'
      units.create unit
      units.first(name: 'Probe').status.should be: 'alive'
      unit.status = 'dead'
      units.update {_id: unit._id}, unit
      units.first(name: 'Probe').status.should be: 'dead'
      units.count().should be: 1

    it.sync "should update in-place", ->
      units = $db.collection 'units'
      units.create name: 'Probe',  status: 'alive'
      units.update({name: 'Probe'}, $set: {status: 'dead'}).should be: 1
      units.first(name: 'Probe').status.should be: 'dead'

    it.sync "should delete", ->
      units = $db.collection 'units'
      units.create name: 'Probe',  status: 'alive'
      units.delete(name: 'Probe').should be: 1
      units.count(name: 'Probe').should be: 0

    it.sync "should update all matched by criteria (not just first as default in mongo)", ->
      units = $db.collection 'units'
      units.save name: 'Probe',  race: 'Protoss', status: 'alive'
      units.save name: 'Zealot', race: 'Protoss', status: 'alive'
      units.update {race: 'Protoss'}, $set: {status: 'dead'}
      units.all().map((u) -> u.status).should be: ['dead', 'dead']
      units.delete race: 'Protoss'
      units.count().should be: 0

    it.sync "should use autogenerated random string id (if specified, instead of default BSON::ObjectId)", ->
      units = $db.collection 'units'
      unit = name: 'Probe',  status: 'alive'
      units.create unit
      _.isString(unit._id).should be: true

  describe "Querying", ->
    it.sync "should return first element", ->
      units = $db.collection 'units'
      _(units.first()).should be: null
      units.save name: 'Zeratul'
      units.first(name: 'Zeratul').name.should be: 'Zeratul'

    it.sync 'should return all elements', ->
      units = $db.collection 'units'
      units.all().should be: []
      units.save name: 'Zeratul'
      list = units.all(name: 'Zeratul')
      list.length.should be: 1
      list[0].name.should be: 'Zeratul'

    it.sync 'should return count of elements', ->
      units = $db.collection 'units'
      units.count(name: 'Zeratul').should be: 0
      units.save name: 'Zeratul'
      units.save name: 'Tassadar'
      units.count(name: 'Zeratul').should be: 1

    it.sync "should delete via cursor", ->
      units = $db.collection 'units'
      units.create name: 'Probe',  status: 'alive'
      units.find(name: 'Probe').delete()
      units.count(name: 'Probe').should be: 0

describe "Driver Configuration", ->
  withMongo()

  oldOptions = null
  beforeEach -> oldOptions = _(mongo.options).clone()
  afterEach  -> mongo.options = oldOptions

  it.sync "should use config and get database by its alias", ->
    config =
      databases:
        mytest:
          name: 'test'
    mongo.configure config

    try
      db = mongo._db('mytest')
      db.name.should be: 'test'
    finally
      db.close() if db